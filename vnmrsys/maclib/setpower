"Donghua H. Zhou/Rienstra NMR Group, UIUC/Apr 12, 2004"

if ($# < 1) then
  clear
  write('alpha','Set fine power according to R3 conditions stored in ~/vnmrsys/probes/'+probe+'/power; or set both fine power and pulse width for hard 90deg pulses with calibration stored in the same probe file. 
_____You MUST properly set the vnmr parameter probe.______
Examples:
setpower(\'aH90\')   retrieve both aH90 and pwH90 
     setpower(\'aC90\')     setpower(\'aN90\') etc.
setpower(\'aX90\')   retrieve values associated with tn
setpower(\'aY90\')   ................................dn2

setpower(\'aHmix\',1) set proton power during mixing to n=1 R3 condition.

setpower(\'aHhy\',3.5) set proton power during HY CP to 3.5 R3 condition
setpower(\'aYhy\',2.5) ......
    unlike setpower(\'aY90\'), the appropriated nucleus is decided
    by the value in parameter toHY (type parcp for more info).')
write('alpha','
Alternatively, you may use this method: aXhx = 2.5* aC, 
aHtppm = 70k*aH/srate, or Array(\'aYyx\',1.1*aN,1.9*aN,100,\'e\'). When you 
assign srate, say, srate=16666, the parameters aH, aC, and aN are updated 
for the n=1 R3 condition for proton, C13, and N15 respectively.')
  return
endif

$temp = ''
substr($1,1,1):$temp
if ($temp <> 'a') then
  write('error','The linear power parameter to be set must begin with letter a!')
  return
endif 

$format = -1
substr($1,2,1):$temp 
$c1='' $c2='' $c3='' $c4=''
substr(tn,1,1):$c1 substr(dn,1,1):$c2 substr(dn2,1,1):$c3 substr(dn3,1,1):$c4
if ($temp = $c1) then  "aH... aC... aN... format #1"
  $nuc = tn  $format = 1
else if ($temp = $c2) then
  $nuc = dn $format = 1
else if ($temp = $c3) then
  $nuc = dn2 $format = 1
else if ($temp = $c4) then
  $nuc = dn3 $format = 1
else if ($temp = 'H') then "some times, dn or dn2 are not defined"
  $nuc = 'H1' $format = 1  "but _srate need to set aH, aC, aN"
else if ($temp = 'C') then
  $nuc = 'C13'$format = 1
else if ($temp = 'N') then
  $nuc = 'N15' $format = 1
endif endif endif
endif endif endif endif

"aX90   format #2"

substr($1,3,2):$temp
if ($format = 1 and $temp = '90') then
  $format = 2
endif
if ($format = -1) then
  if ($temp = '90') then
     $format = 2 
     substr($1,2,1):$c2
     if ($c2 = 'X') then
       $nuc = tn
     else if ($c2 = 'Y') then
       $nuc = dn2
     endif endif
  endif
endif


"aXyx... aYyx ... format in CP, #3"
if ($format = -1) then
  substr($1,2,1):$chnl
  format($chnl,'lower'):$chnl  "X to x"
  substr($1,3,2):$chnls 
  format($chnls,'upper'):$chnls  "YX"
  substr($1,3,1):$c3 substr($1,4,1):$c4
  if ($chnl = $c3) then
    $ch = {'fr'+$chnls}   "frYX = obs ..."
  else if ($chnl = $c4) then
    $ch = {'to'+$chnls}  "toYX"
  endif endif

  if ($ch = 'obs') then
    $nuc = tn
  else if ($ch = 'dec') then
    $nuc = dn
  else if ($ch = 'dec2') then
    $nuc = dn2
  else if ($ch = 'dec3') then
    $nuc = dn3
  endif endif endif endif

  $format = 3
endif  

if ($nuc = '' or $nuc = 'none') then
    write('error', 'Please set nucleus other than \'\' and \'none\'.')
    return
endif


$probefile = userdir+'/probes/'+probe+'/power'
exists($probefile,'file'):$e
if (not $e) then
  write('error','Cannot find file '+$probefile)
  return
endif

"$srate = 0.0   $v1 = 0.0 $v2 = 0.0 $v3 = 0.0 $v4 = 0.0"
lookup('file',$probefile)
lookup('COMMENTEND','srate','read'):$srate

lookup('file',$probefile)
lookup('COMMENTEND',$nuc,'read',4):$v1,$v2,$v3,$v4
" aH1 aH2 aH90 pwH90"

if ($format = 2) then
  {$1} = $v3
  substr($1,2,3):$temp
  {'pw'+$temp} = $v4
else if ($format = 1 or $format = 3) then
"  {$1} = srate/$srate * ($v1 + ($v2-$v1)*($2-1))   linear fitting y = kx+b"
"  {$1} = srate/$srate * $2 * ($v1 + $v2/2.0) /2.0"  "linear fitting y=kx"
   {$1} = srate/$srate * $2 * ($v1 + $v2/2.0 + $srate* $v3 * $v4/250000.0)/3.0   "y=kx"

endif endif

